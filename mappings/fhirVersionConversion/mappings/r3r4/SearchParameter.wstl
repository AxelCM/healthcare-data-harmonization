package fhir_r3r4

import "../ResourceBase.wstl"

// Description: Maps the common fields in FHIR STU3 and FHIR R4
//
// Argument(s):
//   searchParameter : FHIR SearchParameter resource
//
// Output(s):
//   FHIR SearchParameter Resource
//
def SearchParameterBase(searchParameter) {
  resourcebase::ResourceBase(searchParameter)
  url: searchParameter.url
  version: searchParameter.version
  name: searchParameter.name
  date: searchParameter.date
  publisher: searchParameter.publisher
  contact: searchParameter.contact
  description: searchParameter.description
  useContext: searchParameter.useContext
  jurisdiction: searchParameter.jurisdiction
  purpose: searchParameter.purpose
  code: searchParameter.code
  expression: searchParameter.expression
  xpath: searchParameter.xpath
  chain: searchParameter.chain
}

// Description: Converts a FHIR STU3 SearchParameter resource to FHIR R4 SearchParameter resource.
//
// Argument(s):
//   searchParameter : FHIR STU3 SearchParameter resource https://www.hl7.org/fhir/stu3/searchparameter.html
//
// Output(s):
//   FHIR R4 SearchParameter Resource https://www.hl7.org/fhir/r4/searchparameter.html
//
def R3SearchParameterToR4(searchParameter) {
  SearchParameterBase(searchParameter)
  contained: datatypes::R3ResourceToR4(searchParameter.contained[])
  derivedFrom: searchParameter.derivedFrom
  status: searchParameter.status
  experimental: searchParameter.experimental
  base: utilities::Convert_TerminologyCode(searchParameter.base[], "http://hl7.org/fhir/resource-types", "ResourceTypes_r3r4")
  type: searchParameter.type
  xpathUsage: searchParameter.xpathUsage
  target: utilities::Convert_TerminologyCode(searchParameter.target[], "http://hl7.org/fhir/resource-types", "ResourceTypes_r3r4")
  multipleOr: extension::fetchBoolean("https://www.hl7.org/fhir/R4/searchparameter-definitions.html#SearchParameter.multipleOr", searchParameter.extension)
  multipleAnd: extension::fetchBoolean("https://www.hl7.org/fhir/R4/searchparameter-definitions.html#SearchParameter.multipleAnd", searchParameter.extension)
  comparator: searchParameter.comparator
  modifier: searchParameter.modifier
  component: R3SearchParameter_ComponentToR4(searchParameter.component)
}

// Description: Converts a FHIR R4 SearchParameter resource to FHIR STU3 SearchParameter resource.
//
// Argument(s):
//   searchParameter : FHIR R4 SearchParameter resource https://www.hl7.org/fhir/r4/searchparameter.html
//
// Output(s):
//   FHIR STU3 SearchParameter Resource https://www.hl7.org/fhir/stu3/searchparameter.html
//
def R4SearchParameterToR3(searchParameter) {
  SearchParameterBase(searchParameter)
  contained: datatypes::R4ResourceToR3(searchParameter.contained[])
  status: searchParameter.status
  experimental: searchParameter.experimental
  base: searchParameter.base
  type: searchParameter.type
  derivedFrom: searchParameter.derivedFrom
  xpathUsage: searchParameter.xpathUsage
  target: searchParameter.target
  comparator: searchParameter.comparator
  modifier: searchParameter.modifier
  component: R4SearchParameter_ComponentToR3(searchParameter.component)
  extension[]: extension::BuildBooleanExtension("https://www.hl7.org/fhir/R4/searchparameter-definitions.html#SearchParameter.multipleOr", searchParameter.multipleOr)
  extension[]: extension::BuildBooleanExtension("https://www.hl7.org/fhir/R4/searchparameter-definitions.html#SearchParameter.multipleAnd", searchParameter.multipleAnd)
}

// Description: Converts a FHIR STU3 SearchParameter.component to FHIR R4 SearchParameter.component.
//
// Argument(s):
//   data : FHIR STU3 SearchParameter.component https://www.hl7.org/fhir/stu3/searchparameter-definitions.html#SearchParameter.component
//
// Output(s):
//   FHIR R4 SearchParameter.component https://www.hl7.org/fhir/r4/searchparameter-definitions.html#SearchParameter.component
//
def R3SearchParameter_ComponentToR4(data) {
  definition: datatypes::ReferenceToCanonical(data.definition)
  expression: data.expression
}

// Description: Converts a FHIR R4 SearchParameter.component to FHIR STU3 SearchParameter.component.
//
// Argument(s):
//   data : FHIR R4 SearchParameter.component https://www.hl7.org/fhir/r4/searchparameter-definitions.html#SearchParameter.component
//
// Output(s):
//   FHIR STU3 SearchParameter.component https://www.hl7.org/fhir/stu3/searchparameter-definitions.html#SearchParameter.component
//
def R4SearchParameter_ComponentToR3(data) {
  definition: datatypes::CanonicalToReference(data.definition)
  expression: data.expression
}
