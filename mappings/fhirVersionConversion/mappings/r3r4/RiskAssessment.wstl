package fhir_r3r4

import "../ResourceBase.wstl"

// Description: Maps the common fields in FHIR STU3 and FHIR R4
//
// Argument(s):
//   riskAssessment : FHIR RiskAssessment resource
//
// Output(s):
//   FHIR RiskAssessment Resource
//
def RiskAssessmentBase(riskAssessment) {
  resourcebase::ResourceBase(riskAssessment)
  method: riskAssessment.method
  code: riskAssessment.code
  occurrenceDateTime: riskAssessment.occurrenceDateTime
  occurrencePeriod: riskAssessment.occurrencePeriod
  mitigation: riskAssessment.mitigation
}

// Description: Converts a FHIR STU3 RiskAssessment resource to FHIR R4 RiskAssessment resource.
//
// Argument(s):
//   riskAssessment : FHIR STU3 RiskAssessment resource https://www.hl7.org/fhir/stu3/riskAssessment.html
//
// Output(s):
//   FHIR R4 RiskAssessment Resource https://www.hl7.org/fhir/r4/riskAssessment.html
//
def R3RiskAssessmentToR4(riskAssessment) {
  RiskAssessmentBase(riskAssessment)
  contained: datatypes::R3ResourceToR4(riskAssessment.contained[])
  basis: datatypes::R3ReferenceToR4(riskAssessment.basis[])
  condition: datatypes::R3ReferenceToR4(riskAssessment.condition)
  basedOn: datatypes::R3ReferenceToR4(riskAssessment.basedOn)
  parent: datatypes::R3ReferenceToR4(riskAssessment.parent)
  identifier: R3RiskAssessment_IdentifierToR4("https://www.hl7.org/fhir/R4/riskassessment-definitions.html#RiskAssessment.identifier", riskAssessment.extension, riskAssessment.identifier)
  status: riskAssessment.status
  subject: datatypes::R3ReferenceToR4(riskAssessment.subject)
  subject.display: utilities::defaultIfNull($this.subject, "Patient")
  encounter: datatypes::R3ReferenceToR4(riskAssessment.context)
  performer: datatypes::R3ReferenceToR4(riskAssessment.performer)
  reasonCode[]: riskAssessment.reasonCodeableConcept
  reasonReference: extension::fetchReferenceArray("https://www.hl7.org/fhir/R4/riskassessment-definitions.html#RiskAssessment.reasonReference", riskAssessment.extension)
  prediction: riskAssessment.prediction
  note: R3RiskAssessment_NoteToR4("https://www.hl7.org/fhir/R4/riskassessment-definitions.html#RiskAssessment.note", riskAssessment.extension, riskAssessment.comment)
  extension[]: extension::BuildReferenceExtension("https://www.hl7.org/fhir/STU3/riskassessment-definitions.html#RiskAssessment.reasonReference", riskAssessment.reasonReference)
}

// Description: Converts a FHIR R4 RiskAssessment resource to FHIR STU3 RiskAssessment resource.
//
// Argument(s):
//   riskAssessment : FHIR R4 RiskAssessment resource https://www.hl7.org/fhir/r4/riskAssessment.html
//
// Output(s):
//   FHIR STU3 RiskAssessment Resource https://www.hl7.org/fhir/stu3/riskAssessment.html
//
def R4RiskAssessmentToR3(riskAssessment) {
  RiskAssessmentBase(riskAssessment)
  contained: datatypes::R4ResourceToR3(riskAssessment.contained[])
  basis: datatypes::R4ReferenceToR3(riskAssessment.basis[])
  condition: datatypes::R4ReferenceToR3(riskAssessment.condition)
  basedOn: datatypes::R4ReferenceToR3(riskAssessment.basedOn)
  parent: datatypes::R4ReferenceToR3(riskAssessment.parent)
  identifier: riskAssessment.identifier[0]
  status: riskAssessment.status
  subject: datatypes::R4ReferenceToR3(riskAssessment.subject)
  context: datatypes::R4ReferenceToR3(riskAssessment.encounter)
  performer: datatypes::R4ReferenceToR3(riskAssessment.performer)
  reasonCodeableConcept: extension::fetchCodeableConcept("https://www.hl7.org/fhir/STU3/riskassessment-definitions.html#RiskAssessment.reasonCodeableConcept", riskAssessment.extension)
  reasonReference: extension::fetchReference("https://www.hl7.org/fhir/STU3/riskassessment-definitions.html#RiskAssessment.reasonReference", riskAssessment.extension)
  prediction: riskAssessment.prediction
  comment: riskAssessment.note[0].text
  extension[]: extension::BuildIdentifierExtension("https://www.hl7.org/fhir/R4/riskassessment-definitions.html#RiskAssessment.identifier", riskAssessment.identifier[])
  extension[]: extension::BuildCodeableConceptExtension("https://www.hl7.org/fhir/R4/riskassessment-definitions.html#RiskAssessment.reasonCode", riskAssessment.reasonCode[])
  extension[]: extension::BuildReferenceExtension("https://www.hl7.org/fhir/R4/riskassessment-definitions.html#RiskAssessment.reasonReference", riskAssessment.reasonReference[])
  extension[]: extension::BuildAnnotationExtension("https://www.hl7.org/fhir/R4/riskassessment-definitions.html#RiskAssessment.note", riskAssessment.note[])
}

// Description: Converts a FHIR STU3 RiskAssessment.identifier to FHIR R4 RiskAssessment.identifier.
//
// Argument(s):
//   data : FHIR STU3 RiskAssessment.identifier https://www.hl7.org/fhir/stu3/riskassessment-definitions.html#RiskAssessment.identifier
//
// Output(s):
//   FHIR R4 RiskAssessment.identifier https://www.hl7.org/fhir/r4/riskassessment-definitions.html#RiskAssessment.identifier
//
def R3RiskAssessment_IdentifierToR4(url, extension, data) {
  var identifier: extension::fetchIdentifierArray(url, extension)
  if identifier then {
    identifier
  } else {
    arrayOf(data)
  }
}

// Description: Converts a FHIR STU3 RiskAssessment.note to FHIR R4 RiskAssessment.note.
//
// Argument(s):
//   data : FHIR STU3 RiskAssessment.note https://www.hl7.org/fhir/stu3/riskassessment-definitions.html#RiskAssessment.note
//
// Output(s):
//   FHIR R4 RiskAssessment.note https://www.hl7.org/fhir/r4/riskassessment-definitions.html#RiskAssessment.note
//
def R3RiskAssessment_NoteToR4(url, extension, data) {
  var note: extension::fetchAnnotationArray(url, extension)
  if note then {
    note
  } else {
    var noteObject.text: data
    arrayOf(noteObject)
  }
}
